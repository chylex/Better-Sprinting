buildscript{
    repositories{
        maven{ url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    
    dependencies{
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

def mcversion = "1.14.4"
def forgeversion = "28.0.11"

def prefixName = 'displayName = '
def prefixVersion = 'version = '

def metaLines = file('src/main/resources/META-INF/mods.toml').readLines()
def metaName = metaLines.find { line -> line.startsWith(prefixName) }.substring(prefixName.length())[1..-2]
def metaVersion = metaLines.find { line -> line.startsWith(prefixVersion) }.substring(prefixVersion.length())[1..-2]

group = 'chylex.bettersprinting'
version = metaVersion
archivesBaseName = metaName.replaceAll('\\s', '')

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft{
    mappings channel: 'snapshot', version: '20190719-1.14.3'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    
    runs{
        client{
            properties 'bettersprinting.debug': ''
            workingDirectory file('run')
            
            mods{
                bettersprinting{
                    source sourceSets.main
                }
            }
        }
        
        server{
            properties 'bettersprinting.debug': ''
            workingDirectory file('run')
            
            mods{
                bettersprinting{
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies{
    minecraft 'net.minecraftforge:forge:' + mcversion + '-' + forgeversion
}

jar{
    archiveName = archivesBaseName + '-' + mcversion + '-v' + version + '.jar'

    from('./'){
        include 'LICENSE'
    }

    manifest{
        attributes([
            'Specification-Title'  : 'bettersprinting',
            'Specification-Version': '1',
            'Specification-Vendor' : 'chylex',

            'Implementation-Title'  : metaName,
            'Implementation-Version': metaVersion,
            'Implementation-Vendor' : 'chylex',

            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
